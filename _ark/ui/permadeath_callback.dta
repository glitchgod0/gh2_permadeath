#define LOST_THE_GAME
(
   ;lost a life
   {++ $lives_lost}
   ;lives remain, go back to song_select/main menu
   {if {< $lives_lost $num_of_lives}
      {meta_loading_goto permadeath_lost_screen}
   }
   ;no lives remain
   {if {== $num_of_lives $lives_lost}
      ;delete all save files
      {foreach_int $idx 0 MAX_NUM_PROFILES
         {if {! {campaign is_empty_profile $idx}}
            {campaign delete_slot $idx}
         }
      }
      ;submit final note streak
      {set $this_note_streak {+ $this_note_streak $temp_note_streak}}
      ;return to intro flow
      {meta_loading_goto permadeath_lives_screen}
   }
)
#define COMMON_CALLBACK_HANDLES
(
   (gem_hit_callback
      ;track temp note streak in case of life lost
      ;streak is submitted on win song or lost all lives
      {++ $temp_note_streak}
   )
   (gem_miss_callback LOST_THE_GAME)
   (gem_pass_callback LOST_THE_GAME)
)
{new
   Object
   note_streak_callback_p0
   COMMON_CALLBACK_HANDLES
}
{new
   Object
   note_streak_callback_p1
   COMMON_CALLBACK_HANDLES
}